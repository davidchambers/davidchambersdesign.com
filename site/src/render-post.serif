import * from "./elements.serif";
import s from "./sanctuary.serif";
import tags from "./tags.serif";

export default render-post;

render-post post related-posts =
  #[(article'
       (s:maybe #{} (s:singleton :id) (s:value :article-id post))
       #[(header
            #[(h1 post:title)
              (time #{:datetime post:datetime.toFormat("yyyy-MM-dd'T'HH:mm:ssZZ") :pubdate "pubdate"}
                 post:datetime.toFormat("d MMMM y"))])
         ...post:body
         (footer' #{:class "metadata"}
            (s:join #[#[(ul (li' #{:class "shorturl"} (a ["http://dÄ‹d.ws/" + post:id + "/"] "Short URL")))]
                      (s:array #[]
                               (head tail ->
                                  #[(h4 "This post has the following tags:")
                                    (ol (s:map (tag -> (li (a ["/tag/" + tag + "/"] tags[tag])))
                                               (s:prepend head tail)))])
                               post:tags)]))
         ...{
           if related-posts.length === 0 then #[] else #[
             (h3' #{:id "related"} "Possibly related posts")
             (ul (s:map (related-post ->
                           (li (a ["/" + related-post:slug + "/"] related-post:title)))
                        related-posts))
           ]
         }])]
