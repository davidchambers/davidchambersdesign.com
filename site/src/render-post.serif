import * from "./elements.serif";
import tags from "./tags.serif";

export default render-post;

render-post post related-posts = [
  article' {if post.article-id == undefined then {} else {id: post.article-id}} [
    header [
      h1 post.title,
      time {
        datetime: .toFormat ["yyyy-MM-dd'T'HH:mm:ssZZ"] post.datetime,
        pubdate: "pubdate",
      } [
        .toFormat ["d MMMM y"] post.datetime,
      ],
    ],
    ...post.body,
    footer' {class: "metadata"} [
      ul [
        li' {class: "shorturl"} [
          a {href: "http://dÄ‹d.ws/" + post.id + "/"} ["Short URL"],
        ],
      ],
      ...{
        if post.tags == [] then [] else [
          h4 ["This post has the following tags:"],
          ol $ map (\tag -> li [a {href: "/tag/" + tag + "/"} [tags[tag]]]) post.tags,
        ]
      },
    ],
    ...{
      if related-posts == [] then [] else [
        h3' {id: "related"} ["Possibly related posts"],
        ul $ map (\post -> li [a {href: "/" + post.slug + "/"} post.title]) related-posts,
      ]
    },
  ],
];
