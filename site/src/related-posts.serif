import sanctuary from "sanctuary";

export default related-posts;

S = sanctuary.unchecked;

related-posts posts post = {
  tags = Set.new post.tags;
  posts
  & S.mapMaybe (\self ->
      if self.slug == post.slug then S.Nothing else {
        dividend = (.length) $ filter (\tag -> tags has tag) self.tags;
        divisor = Math.sqrt $ (.size) $ Set.new $ post.tags <> self.tags;
        score = dividend / divisor;
        if score < 0.5 then S.Nothing else S.Just {...self, score}
      }
    )
  & S.sortBy (\self ->
      .diff [post.datetime] self.datetime
      & (.milliseconds)
      & Math.abs
      & S.Pair (-self.score)
    )
  & sliceTo 5
};
