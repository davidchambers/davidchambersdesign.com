import S from "sanctuary";

import * from "./elements.serif";

export default render-archives;

render-post {slug, title, datetime} = {
  li [
    a {href: `/${slug}`} title,
    " ",
    time {datetime: .toISO [] datetime} [
      {datetime |> .toFormat ["d MMMM y | h:mm"]} +
      {datetime |> .toFormat ["a"] |> .toLowerCase []},
    ],
  ]
};

render-section posts = {
  li [
    h2 [posts[0].formatted-date],
    ol $ render-post <$> posts,
  ]
};

render-archives posts = {
  posts
  |> map (\post -> {...post, formatted-date: .toFormat ["MMMM y"] post.datetime})
  |> S.sortBy (\post -> -post.datetime)
  |> S.groupBy (\this that -> this.formatted-date is that.formatted-date)
  |> map render-section
  |> \archives -> [
    h1 ["Archives"],
    ol' {class: "archives"} archives,
  ]
};
