import * from "serif/es"

import * from "./elements.serif"
import default as s from "./sanctuary.serif"

export default render-archives

render-section section =
  (li
     #[(h2 (:heading section))
       (ol
          (s:map (post ->
                    (li
                       #[(a' #{:href "/" + (:slug post)} (:title post))
                         " "
                         (time #{:datetime (.toISO (:datetime post))}
                            (.toFormat "d MMMM y | h:mm" (:datetime post))
                            + (s:to-lower (.toFormat "a" (:datetime post))))]))
                 (:posts section)))])

render-archives =
  (s:pipe #[(s:map (s:join s:Pair))
            (s:map (s:map-left (:datetime _)))
            (s:sort-by (s:compose (s:compose s:negate Number) s:fst))
            (s:map (s:map-left (.toFormat "MMMM y" _)))
            (s:group-by (s:on s:equals s:fst))
            (s:chain (s:array #[] (head tail -> #[#{:heading (s:fst head) :posts (s:map s:snd (s:prepend head tail))}])))
            (sections ->
               #[(h1 "Archives")
                 (ol' #{:class "archives"}
                    (s:map render-section sections))])])
