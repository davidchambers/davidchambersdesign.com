import * from "./elements.serif"
import s from "./sanctuary.serif"

export {
  caption
  captioned-image
  captioned-images
  code-block
  decorative-image
  interview-list
  pros-and-cons-list
  uncaptioned-image
  update
}

caption caption =
  (p' #{:class "caption"} caption)

captioned-image src alt caption =
  (dl
     #[(dt (img #{:alt alt :src src}))
       (dd caption)])

captioned-images images =
  (dl (s:chain (image -> #[(dt (img #{:alt image[1] :src image[0]}))
                           (dd image[2])])
               images))

code-block language source-code = {
  lines =
    (s:from-maybe #[] (s:chain s:init (s:tail (s:lines source-code))))
  trim-leading-spaces line =
    (s:from-maybe line
                  (s:chain (s:strip-prefix _ line)
                           (s:map (x -> x.match)
                                  (s:chain (s:match (s:regex "" "^[ ]*"))
                                           (s:head lines)))))
  (pre (code (text (s:unlines (s:map trim-leading-spaces lines)))))
}

decorative-image src =
  (p (img #{:alt "" :src src}))

interview-list interviewer interviewee exchange =
  (ol (s:snd (s:reduce (s:pair (name items quotation -> {
                                  if name === interviewer then {
                                    (s:Pair interviewee
                                            #[...items
                                              (li' #{:class "interviewer"}
                                                 #[(strong interviewer + ":") " " ...quotation])])
                                  } else {
                                    (s:Pair interviewer
                                            #[...items
                                              (li' #{}
                                                 #[(strong interviewee + ":") " " ...quotation])])
                                  }
                                }))
                       (s:Pair interviewer #[])
                       (s:map canonicalize-children exchange))))

pros-and-cons-list f =
  (ul (f (li' #{:class "pro"})
         (li' #{:class "con"})))

uncaptioned-image src alt =
  (p (img #{:alt alt :src src}))

update datetime body =
  (div #{:class "update"}
     (s:prepend (h4 #["Update \u2014 "
                      (time #{:datetime (.toISO datetime)}
                         (.toFormat "d MMMM y" datetime))])
                (canonicalize-children body)))
